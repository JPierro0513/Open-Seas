title: Start.1.1.1
tags: 
colorID: 0
position: -1248,303
---
<<if $ID1 is true>>
Pierce Blakely: You don't need to check in with me, Taggert. I'm alright. Take care of the rest of the crew.
<<else>>
<<set $ES1 to "Pleased">>
<<set $CT1 to 0>>
<<set $pq1 to false>>
...
[[You asked to see me?|Continue.1.1.1]]
<<endif>>
===
title: Respectful.1.1.1
tags: 
colorID: 0
position: -1435,1925
---
Pierce Blakely: How many times do I have to tell you, it's Pierce, or Captain Blakely if you really must.
[[Of course it is, sir.|Respectful.1.1.2]]
[[Of course, Captain Blakely|Respectful.Mildly.1.1.1]]
[[Of course, Pierce. Just pulling your leg.|Flippant.1.1.2]]
===
title: Respectful.1.1.2
tags: 
colorID: 0
position: -1842,2028
---
<<set $ES1 to "Frustrated">>
<<set $PierceSir to true>>
Pierce looks frustrated.
Pierce Blakely: Oh, for the love of- Thank you, Quartermaster, that will be all.
<<set $ID1 to true>>
===
title: Respectful.Mildly.1.1.1
tags: 
colorID: 0
position: -1568,2262
---
Pierce Blakely: Very well, Quartermaster Taggert. Good luck with the rest of the crew.
<<set $ID1 to true>>
===
title: Flippant.1.1.2
tags: 
colorID: 0
position: -1274,2267
---
Pierce Blakely: Of course you are. Well, if you're done pulling one over on your beloved captain, please go talk to the rest of the crew. Good luck, Adeline.
<<set $ID1 to true>>
===
title: Continue.1.1.1
tags: 
colorID: 0
position: -1261,595
---
Pierce Blakely: ...
[[Sir?|Continue.Sir.1.1.2]]
[[Captain Blakely|Continue.1.1.2]]
[[Pierce?|Continue.1.1.2]]
===
title: Continue.1.1.2
tags: 
colorID: 0
position: -1419,879
---
Pierce Blakely: Can you see it out there, Taggert?
[[I'm not sure what you mean.|Continue.1.1.3]]
===
title: Continue.Sir.1.1.2
tags: 
colorID: 0
position: -1127,880
---
Pierce Blakely: Can you see it out there, Taggert?
[[I'm not sure what you mean, sir.|Continue.1.1.3]]
===
title: Continue.1.1.3
tags: 
colorID: 0
position: -1245,1134
---
Pierce Blakely: Sometimes, when we’re moving quickly enough through the waters, I think I catch a glimpse of it. It’s never longer than that, but Adeline, it is glorious. When we’ve been having a few hard weeks I try to spend more time out here, to remember what this is all for.
Pierce Blakely: You know, for all the time I’ve known you, I don’t know if we’ve ever talked about this. What do you see when you think of Libertalia, Taggert? The idea of a pirate haven can be quite different for different people.
[[I see a place where we can be free.|Free.1.1.1]]
[[I see a place where we can be safe.|Safe.1.1.1]]
[[I see... an island? It's kind of hard to think that far ahead.|Unsure.1.1.1]]
===
title: Free.1.1.1
tags: 
colorID: 0
position: -1539,1418
---
<<set $CT1 to $CT1 + 1>>
Pierce Blakely: A worthy goal. That’s the epitome of piracy, isn’t it? The real draw for most, no matter what they say about us. I must say, that is what I imagine as well, somewhere we can be free of our responsibilities, of the mistakes and pain of our pasts. 
Pierce Blakely: Somewhere the British, or the Spanish, or whatever great empires replace them can no longer reach us.
[[Continue|Continue.1.1.4]]
===
title: Safe.1.1.1
tags: 
colorID: 0
position: -1228,1417
---
<<set $CT1 to $CT1 + 1>>
Pierce Blakely: Ah, who isn’t running from something? The ability to put down our swords, lower the flags… I must admit I can’t even begin to imagine it. But to give a place for those who do not wish to fight to live in peace? Yes, a powerful idea Taggert.
[[Continue|Continue.1.1.4]]
===
title: Unsure.1.1.1
tags: 
colorID: 0
position: -910,1389
---
<<set $CT1 to $CT1 - 1>>
Pierce Blakely: Well, I suppose that’s why I put you forward for Quartermaster, isn’t it? To balance me out? Still, I would take some time occasionally to think about what you want from all this. Pragmatism is all well and good, but without the end goal in mind, what is the point of it?
[[Continue|Continue.1.1.4]]
===
title: Continue.1.1.4
tags: 
colorID: 0
position: -1225,1679
---
Pierce Blakely: Regardless, that’s not why I called you over here. We've been away from Nassau for quite a while, if you haven't noticed. A necessity, of course, but I worry that some of the crew may be growing restless. It may be a far cry from Libertalia, but it’s the closest thing most of them have had to a home in a very long time.
Pierce Blakely: I believe part of your job description is to handle that sort of thing, unless I'm very much mistaken.
<<if $pq1 is false>>
[[Is it home for you?|Personal.1.1.1]]
<<endif>>
[[I'll check in with them, sir|Respectful.1.1.1]]
[[I'll check in with them. How are you handling it Pierce?|Personal.1.1.2]]
===
title: Personal.1.1.1
tags: 
colorID: 0
position: -1566,1680
---
Pierce Blakely: The Bittersweet is my home, Taggert. But Nassau… Perhaps, with time. Maybe I’m so used to chasing the idea of Libertalia that I’ve missed that it’s already here. But to be honest, I doubt it.
<<set $pq1 to true>>
[[Continue|Continue.1.1.4]]
===
title: Personal.1.1.2
tags: 
colorID: 0
position: -1113,1941
---
<<set $CT1 to $CT1 + 1>>
Pierce Blakely: You don't need to check in with me, Adeline, though I appreciate it. I'm alright. Take care of the rest of the crew.
<<set $ID1 to true>>
===